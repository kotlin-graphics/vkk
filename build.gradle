import org.gradle.internal.os.OperatingSystem

plugins {
    id 'maven'
    id 'java'
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.3.10"
//    id "org.jetbrains.kotlin.kapt" version "1.3.10"
    id "com.github.johnrengelman.shadow" version '2.0.4'
}

// jitpack
group = 'com.github.kotlin-graphics'

ext.kotlin_version = '1.3.10'
ext.moduleName = 'com.github.kotlin_graphics.vkk'

dependencies {

    ext.kotlin = "org.jetbrains.kotlin:kotlin"
    implementation "$kotlin-stdlib:$kotlin_version".toString()
    implementation "$kotlin-reflect:$kotlin_version".toString()

    ext.kx = "com.github.kotlin-graphics"
    api "$kx:gli:bded5be73ebedf23e236b1dbf47151cb98695e66".toString()

//    testCompile 'io.kotlintest:kotlintest-runner-junit5:3.0.6'

    ext.lwjglVersion = "3.2.1-SNAPSHOT"
    switch (OperatingSystem.current()) {
        case OperatingSystem.WINDOWS:
            ext.lwjglNatives = "natives-windows"
            break
        case OperatingSystem.LINUX:
            ext.lwjglNatives = "natives-linux"
            break
        case OperatingSystem.MAC_OS:
            ext.lwjglNatives = "natives-macos"
            break
    }
    ["", "-glfw", "-jemalloc", "-openal", "-opengl", "-stb", "-vulkan"].each {
        implementation "org.lwjgl:lwjgl$it:$lwjglVersion"
        if (it != "-vulkan")
            runtime "org.lwjgl:lwjgl$it:$lwjglVersion:$lwjglNatives"
    }

    ext.spirvCross = "graphics.scenery:spirvcrossj:0.4.0"
    implementation "$spirvCross".toString()
    runtime "$spirvCross:$lwjglNatives".toString()

//    compile group: 'org.jetbrains.kotlin.kapt', name: 'org.jetbrains.kotlin.kapt.gradle.plugin', version: '1.3.0-rc-146'
}

repositories {
    mavenCentral()
//    maven { setUrl("https://dl.bintray.com/kotlin/kotlin-eap") }
    maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    maven { setUrl('https://jitpack.io') }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    inputs.property("moduleName", moduleName)
    manifest.attributes('Automatic-Module-Name': moduleName)
}